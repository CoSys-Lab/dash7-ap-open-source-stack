#Export the interface of the HAL to the rest of the code base
GLOBAL_INCLUDE_DIRECTORIES("inc")

#Export the 'binary' platform directory so the 'framework_defs.h' file
#generated by FRAMEWORK_BUILD_SETTINGS file can be found
GLOBAL_INCLUDE_DIRECTORIES(${CMAKE_CURRENT_BINARY_DIR})

#Include framework-specific helper MACRO's
include(${CMAKE_SOURCE_DIR}/cmake/framework_macros.cmake)

#add the hal
ADD_SUBDIRECTORY("hal")
#add the non-hal components
ADD_SUBDIRECTORY("components")

#Add framework options && import them in the platform_defs.h file
SET(SCHEDULER_MAX_TASKS "64" CACHE STRING "The maximum number of tasks that can be registered with the scheduler")
FRAMEWORK_HEADER_DEFINE(NUMBER SCHEDULER_MAX_TASKS)

SET(SCHEDULER_LOWPOWER_MODE "0" CACHE STRING "The low power mode to use. Only change this if you know exactly what you are doing")
FRAMEWORK_HEADER_DEFINE(NUMBER SCHEDULER_LOWPOWER_MODE)

SET(LOG_BINARY "FALSE" CACHE BOOL "Use binary logging (where availabe)")
FRAMEWORK_HEADER_DEFINE(BOOL LOG_BINARY)

SET(LOGGING_ENABLED "TRUE" CACHE BOOL "Select whether to enable or disable the generation of logs")
FRAMEWORK_HEADER_DEFINE(BOOL LOGGING_ENABLED)

SET(TIMER_STACK_SIZE "20" CACHE STRING "The number of simultaneous timer events that can be scheduled. Increase this if you have lots of concurrent timers")
FRAMEWORK_HEADER_DEFINE(NUMBER TIMER_STACK_SIZE)


#Generate the 'framework_defs.h'
FRAMEWORK_BUILD_SETTINGS_FILE()

SET(FRAMEWORK_SOURCES framework_bootstrap.c)
# Assemble the library
# PLATFORM_CHIP_LIBS: Object libraries of individual chips
# PLATFORM: Object library of the platform itself
# FRAMEWORK_OVERRIDE_LIBS: non-hal object libraries that were overridden by the HAL
# FRAMEWORK_LIBS: non-hal object libraries (not overridden)
SET(LIBNAMES ${PLATFORM_CHIP_LIBS} "PLATFORM" ${FRAMEWORK_OVERRIDE_LIBS} ${FRAMEWORK_LIBS})

#Some wizardry to get ADD_LIBRARY to accept everything
FOREACH(libname ${LIBNAMES})
    LIST(APPEND LIBS "$<TARGET_OBJECTS:${libname}>")
ENDFOREACH()
#AND finally add the library
ADD_LIBRARY(framework STATIC ${LIBS} ${FRAMEWORK_SOURCES})
